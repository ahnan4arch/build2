# file      : libbuild2/build/script/parser+line.test.testscript
# license   : MIT; see accompanying LICENSE file

test.options += -d

: if-else
:
$* <<EOF >>EOF
  if foo
    bar
  elif fox
    if fix
      baz
    end
    biz
  end
  if! foo
    bar
  elif! fox
    baz
  end
  EOF

: command
:
$* <<EOF >>EOF
  foo >| 2>- &a &?b
  foo >=c 2>~/error:.*/ &!c
  foo >>:/~%EOS%
  %.*
  abc
  %xyz.*%
  EOS
  EOF

: quoting
:
$* <<EOF >>EOF
  foo 'bar' "baz" '' ""
  EOF

: partial-quoting
:
$* <<EOI >>EOO
  "$foo"
  "foo$"
  "fo"o
  "foo"\"
  "foo\\"
  fo\"o
  fo\\o
  'fo\"o'
  f"oo" "ba"r
  EOI
  "$foo"
  "foo$"
  "foo"
  "foo\""
  "foo\\"
  fo\"o
  fo\\o
  'fo\"o'
  "foo bar"
  EOO
